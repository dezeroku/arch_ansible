# Forked from https://aur.archlinux.org/packages/wlvncc-git
# and switched to Im-0xea's fork with inhibit_input support
# Plus some patches for the configuration
pkgname=wlvncc-im-0xea-git
_pkgname=wlvncc
pkgver=r
pkgrel=1
commit="ae67b74a39f014029c06f326817c9e2db3258fe6"
pkgdesc='Wayland native VNC client'
arch=(x86_64)
url=https://github.com/Im-0xea/wlvncc
license=(custom:ISC)
depends=(aml libvncserver libxkbcommon libdrm ffmpeg mesa pixman wayland)
makedepends=(git meson wayland-protocols)
# TODO: add a patch that automatically enable the inhibit when full screened
# also add a patch that shows the inhibition status
source=("git+$url#commit=$commit" "change_inhibit_keybind.patch" "dont_inhibit_on_focus.patch")
sha512sums=('SKIP'
            'c2a4671ecff9dad2bee2fed915a1ed37d117849a4738f989c9e49dbf72a46ca7d202785f8fccf9970c7c6d90aa6734162b6b596c1d497ff92364e7e28acb5cf2'
            '4f7d5dd1e0b4ce5f28fecbde06209879b6a9e5b6fd432480f878aac88f8e01c9b8ba68df0972867db40194a9be0981a15c7a373f057a482c8ae144233c70fe0f')
conflicts=(wlvncc)
provides=(wlvncc)

pkgver() {
  cd "$_pkgname"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
    cd "$_pkgname"
    patch --forward --strip=1 --input="${srcdir}/change_inhibit_keybind.patch"
    patch --forward --strip=1 --input="${srcdir}/dont_inhibit_on_focus.patch"
}

build() {
    arch-meson wlvncc build
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
    install -Dm 644 wlvncc/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname
}
